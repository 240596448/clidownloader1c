Перем ВыводВозможен;
Перем ЗапоминатьПредыдущееЗначениеЦвета;

Процедура Вывести(Знач ФорматированнаяСтрока, Знач ЦветТекста = Неопределено) Экспорт
	
	ТаблицаПолей = РазобратьСтроку(ФорматированнаяСтрока, ЦветТекста);

	Если ВыводВозможен Тогда
		
		Консоль = Новый Консоль();
		Если ЗапоминатьПредыдущееЗначениеЦвета Тогда
			ПредыдущееЗначениеЦветаТекта = Консоль.ЦветТекста;
		Иначе
			ПредыдущееЗначениеЦветаТекта = ЦветКонсоли.Белый;	
		КонецЕсли;
		
		Если ЦветТекста = Неопределено Тогда
			ЦветТекста = ПредыдущееЗначениеЦветаТекта;
		КонецЕсли;
		Для Каждого Поле Из ТаблицаПолей Цикл
			Если ЗначениеЗаполнено(Поле.Цвет) Тогда
				Консоль.ЦветТекста = ЦветКонсоли[Поле.Цвет];
			Иначе
				Консоль.ЦветТекста = ПредыдущееЗначениеЦветаТекта;
			КонецЕсли;
			Консоль.Вывести(Поле.Текст);
		КонецЦикла;
		
		Консоль.ЦветТекста = ПредыдущееЗначениеЦветаТекта;
		
		Консоль.Вывести(Символы.ПС);

	Иначе
		
		МассивПолей = ТаблицаПолей.ВыгрузитьКолонку("Текст");
		НеФорматированнаяСтрока = СтрСоединить(МассивПолей);
		
		Сообщить(НеФорматированнаяСтрока);

	КонецЕсли;

КонецПроцедуры

Функция РазобратьСтроку(ФорматированнаяСтрока, ЦветТекста)

	Длина = СтрДлина(ФорматированнаяСтрока);
	
	Таблица = Новый ТаблицаЗначений();
	Таблица.Колонки.Добавить("Текст");
	Таблица.Колонки.Добавить("Цвет");

	тзЦветныеПоля = ЦветныеПоля(ФорматированнаяСтрока);

	Поз = 1;
	Для Каждого ЦветПоле Из тзЦветныеПоля Цикл
		Если Поз < ЦветПоле.Позиция Тогда
			Текст = Сред(ФорматированнаяСтрока, Поз, ЦветПоле.Позиция - Поз + 1);
			Стр = Таблица.Добавить();
			Стр.Текст = Текст;
			Стр.Цвет = ЦветТекста;
		КонецЕсли;
		
		Стр = Таблица.Добавить();
		Стр.Текст = ЦветПоле.Текст;
		Стр.Цвет = ЦветПоле.Цвет;
		
		Поз = ЦветПоле.Позиция + ЦветПоле.Длина + 1;
	КонецЦикла;

	Если Поз-1 <= Длина Тогда
		Текст = Сред(ФорматированнаяСтрока, Поз);
		Стр = Таблица.Добавить();
		Стр.Текст = Текст;
		Стр.Цвет = ЦветТекста;
	КонецЕсли;
	
	Возврат Таблица;
	
КонецФункции

Функция ЦветныеПоля(ФорматированнаяСтрока)
	
	Выражение = "\((.+?)\|#color=(.+?)\)";
	РВ = Новый РегулярноеВыражение(Выражение);
	Совпадения = РВ.НайтиСовпадения(ФорматированнаяСтрока);

	ЦветныеПоля = Новый ТаблицаЗначений;
	ЦветныеПоля.Колонки.Добавить("Позиция");
	ЦветныеПоля.Колонки.Добавить("Текст");
	ЦветныеПоля.Колонки.Добавить("Длина");
	ЦветныеПоля.Колонки.Добавить("Цвет");
	
	Для Каждого Совпадение Из Совпадения Цикл
		Группы = Совпадение.Группы;
		стр = ЦветныеПоля.Добавить();
		стр.Позиция = Совпадение.Индекс;
		стр.Текст = Группы[1].Значение;
		стр.Длина = Совпадение.Длина;
		ЦветИмя = Группы[2].Значение;
		стр.Цвет = ЦветИмя;
	КонецЦикла;
	
	ЦветныеПоля.Сортировать("Позиция");
	
	Возврат ЦветныеПоля;

КонецФункции

Процедура УстановитьВозможностьИспользованияЦветногоВывода()
	
	// возможно понадобится вообще запрещать цветной вывод
	ВыводВозможен = Истина;

	Попытка
		Консоль = Новый Консоль();
		Цвет = Консоль.ЦветТекста;
		ЗапоминатьПредыдущееЗначениеЦвета = Истина;
	Исключение
		ЗапоминатьПредыдущееЗначениеЦвета = Ложь;
	КонецПопытки;
	
КонецПроцедуры

УстановитьВозможностьИспользованияЦветногоВывода();

// Пример форматирования вывода
// Вывести("Оу! (Привет!|#color=Красный) Кажется - я ('цветной'|#color=Малиновый). Как (дела|#color=Желтый)???", "Синий");
