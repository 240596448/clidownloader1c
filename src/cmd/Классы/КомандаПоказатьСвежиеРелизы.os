#Использовать asserts
#Использовать fs
#Использовать strings
#Использовать "../../core"

Перем ПутьВывода;
Перем ПодкаталогРазделов;

///////////////////////////////////////////////////////////////////////////////////////////////////
// Прикладной интерфейс

Процедура ОписаниеКоманды(Команда) Экспорт

	Команда.Опция("d days", 2, "показывать свежие релизы за # дней")
			.ТЧисло();

	Команда.Опция("t testing", Ложь, "выводить информацию о тестовых релизах")
			.ТБулево()
			.ПоУмолчанию(Ложь);

	Команда.Опция("o out", ".", "каталог сохранения скачиваемых файлов или сформированого отчета")
				.ТСтрока();

КонецПроцедуры

// Выполняет логику команды
// 
// Параметры:
//   ПараметрыКоманды - Соответствие ключей командной строки и их значений
//
Функция ВыполнитьКоманду(Знач Команда) Экспорт

	Логин = Команда.ЗначениеОпции("login");
	Пароль = Команда.ЗначениеОпции("password");

	КоличествоДней = Команда.ЗначениеОпции("days");
	ВыводитьТестовые = Команда.ЗначениеОпции("testing");

	ПутьВывода = Команда.ЗначениеОпции("out");

	Сессия = Новый СоединениеССайтом(ПараметрыПриложения.АдресСайта(), Логин, Пароль);	
	Результат = Сессия.ПолучитьСодержимое("/total");
	
	Ожидаем.Что(Результат.ЭтоФайл).Равно(Ложь);
	//Результат = Новый Структура("ТекстСтраницы", Служебный.ФайлВТекст(Служебный.ПараметрыОтладки().ИмяОтладочногоФайла));
	
	ТитульнаяСтраница = Новый ТитульнаяСтраницаРазделы(Результат.ТекстСтраницы);
	Таблица = ТитульнаяСтраница.ПолучитьТаблицу(КоличествоДней, ВыводитьТестовые);
	
	ПодкаталогРазделов = ОбъединитьПути(ПутьВывода, "group");
	ФС.ОбеспечитьКаталог(ПодкаталогРазделов);
	СформироватьВыводТаблицы(Таблица);
	
КонецФункции

Процедура СформироватьВыводТаблицы(Таблица)
	
	ИмяФайла = ОбъединитьПути(ПутьВывода, "Title.md");

	ЗаписьТекста = Новый ЗаписьТекста(ИмяФайла, КодировкаТекста.UTF8);
	ЗаписьТекста.ЗаписатьСтроку("1С:Обновление программ");
	ЗаписьТекста.ЗаписатьСтроку("===");
	
	Поля = "Наименование";
	сткМаксДлины = ВычислитьМаксимальныеДлиныПолей(Таблица, Поля);
	
	ЗаписьТекста.ЗаписатьСтроку("| Код    | Наименование | Количество (в т.ч. свежие) | Последние изменения |");
	ЗаписьТекста.ЗаписатьСтроку("|:------:| ------------ |:--------------------------:| ------------------- |");
	
	Для Каждого Раздел Из Таблица Цикл
		
		СформироватьВыводРаздела(Раздел);

		ТаблицаПроектов = Раздел.ТаблицаПроектов;
		
		Наименование = Раздел.Наименование;
		Шаблон = "| %4 | %1 | %2 | %3 |";
		Строка  = СтрШаблон(Шаблон, 
					ДополнитьПробелами(Наименование, сткМаксДлины.Наименование), 
					ДополнитьПробелами(ТаблицаПроектов.Количество(), 3), 
					"   ", 
					ДополнитьПробелами("[" + Раздел.Код + "]", 6));	
		ЗаписьТекста.ЗаписатьСтроку(Строка);

	КонецЦикла;
	
	ЗаписьТекста.ЗаписатьСтроку("");
	// пропишем ссылки
	Для Каждого Раздел Из Таблица Цикл
		ИмяФайла = ОбъединитьПути("group", Раздел.Код + ".md");
		ЗаписьТекста.ЗаписатьСтроку("["+Раздел.Код+"]: " + ИмяФайла + "  ");
	КонецЦикла;
	
	ЗаписьТекста.Закрыть();

КонецПроцедуры

Процедура СформироватьВыводРаздела(Раздел)
	
	ИмяФайла = ОбъединитьПути(ПодкаталогРазделов, Раздел.Код + ".md"); 
	Наименование = Раздел.Наименование;
	ТаблицаПроектов = Раздел.ТаблицаПроектов;

	ЗаписьТекста = Новый ЗаписьТекста(ИмяФайла, КодировкаТекста.UTF8);
	ЗаписьТекста.ЗаписатьСтроку(Наименование);
	ЗаписьТекста.ЗаписатьСтроку("===");
	ЗаписьТекста.ЗаписатьСтроку("");
	ШаблонСтроки = "| %1 | %2 | %3 |";
	
	Поля = "Наименование,Релиз,Дата";
	сткМаксДлины = ВычислитьМаксимальныеДлиныПолей(ТаблицаПроектов, Поля);
	
	ЗаписьТекста.ЗаписатьСтроку(СтрШаблон(ШаблонСтроки, 
								ДополнитьПробелами("Наименование", сткМаксДлины.Наименование), 
								ДополнитьПробелами("Релиз", сткМаксДлины.Релиз), 
								ДополнитьПробелами("Дата", сткМаксДлины.Дата)));
	ЗаписьТекста.ЗаписатьСтроку(СтрШаблон(ШаблонСтроки,
								СтроковыеФункции.СформироватьСтрокуСимволов("-", сткМаксДлины.Наименование),
								СтроковыеФункции.СформироватьСтрокуСимволов("-", сткМаксДлины.Релиз),
								СтроковыеФункции.СформироватьСтрокуСимволов("-", сткМаксДлины.Дата)));

	Для Каждого Проект Из ТаблицаПроектов Цикл
		Строка = СтрШаблон(ШаблонСтроки, 
				ДополнитьПробелами(Проект.Наименование, сткМаксДлины.Наименование), 
				ДополнитьПробелами(Проект.Релиз, сткМаксДлины.Релиз),
				ДополнитьПробелами(Проект.Дата, сткМаксДлины.Дата));
		ЗаписьТекста.ЗаписатьСтроку(Строка);
	КонецЦикла;
	
	ЗаписьТекста.Закрыть();

КонецПроцедуры

Функция ДополнитьПробелами(Знач Строка, ДоДлины)
	Длина = СтрДлина(Строка);
	КоличествоПробелов = Макс(0, ДоДлины - Длина);
	Пробелы = СтроковыеФункции.СформироватьСтрокуСимволов(" ", КоличествоПробелов);
	Возврат Строка(Строка) + Пробелы;
КонецФункции

Функция ВычислитьМаксимальныеДлиныПолей(Коллекция, Поля)
	
	сткМаксДлины = Новый Структура(Поля);
	Для каждого КЗ Из сткМаксДлины Цикл
		сткМаксДлины.Вставить(КЗ.Ключ, 0);
	КонецЦикла;

	Для Каждого Проект Из Коллекция Цикл
		Для каждого КЗ Из сткМаксДлины Цикл
			сткМаксДлины[КЗ.Ключ] = Макс(КЗ.Значение, СтрДлина(Проект[КЗ.Ключ]));
		КонецЦикла;
	КонецЦикла;

	Возврат сткМаксДлины;

КонецФункции