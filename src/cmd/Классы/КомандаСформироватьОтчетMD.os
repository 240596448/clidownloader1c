#Использовать asserts
#Использовать fs
#Использовать strings
#Использовать "../../core"

Перем ПутьВывода;
Перем ПодкаталогРазделов;

///////////////////////////////////////////////////////////////////////////////////////////////////
// Прикладной интерфейс

Процедура ОписаниеКоманды(Команда) Экспорт

	Команда.Опция("o out", ".", "каталог сохранения скачиваемых файлов или сформированого отчета")
				.ТСтрока();

	Команда.Опция("t testing", Ложь, "выводить информацию о тестовых релизах")
			.ТБулево()
			.ПоУмолчанию(Ложь);

КонецПроцедуры

// Выполняет логику команды
Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	Логин = Команда.ЗначениеОпции("login");
	Пароль = Команда.ЗначениеОпции("password");

	ВыводитьТестовые = Команда.ЗначениеОпции("testing");

	ПутьВывода = Команда.ЗначениеОпции("out");

	Если НЕ Служебный.РежимОтладки() Тогда
		Сессия = Новый СоединениеССайтом(ПараметрыПриложения.АдресСайта(), Логин, Пароль);	
		Результат = Сессия.ПолучитьСодержимое("/total");
		Ожидаем.Что(Результат.ЭтоФайл).Равно(Ложь);
	Иначе
		Результат = Новый Структура("ТекстСтраницы", Служебный.ФайлВТекст(Служебный.ПараметрыОтладки().ИмяОтладочногоФайла));
	КонецЕсли;
	
	ТитульнаяСтраница = Новый ТитульнаяСтраницаРазделы(Результат.ТекстСтраницы);
	Таблица = ТитульнаяСтраница.ПолучитьТаблицу();
	
	ПодкаталогРазделов = ОбъединитьПути(ПутьВывода, "group");
	ФС.ОбеспечитьКаталог(ПодкаталогРазделов);
	СформироватьВыводТаблицы(Таблица);
	
	Сообщить("Отчет сформирован: " + ОбъединитьПути(".", ПутьВывода, "Title.md"));

КонецПроцедуры

Процедура СформироватьВыводТаблицы(Таблица)
	
	ИмяФайла = ОбъединитьПути(ПутьВывода, "Title.md");

	ЗаписьТекста = Новый ЗаписьТекста(ИмяФайла, КодировкаТекста.UTF8);
	ЗаписьТекста.ЗаписатьСтроку("1С:Обновление программ");
	ЗаписьТекста.ЗаписатьСтроку("===");
	ШаблонСтроки = "| %1 | %2 | %3 | %4 |";
	
	Поля = "Наименование";
	сткМаксДлины = ВычислитьМаксимальныеДлиныПолей(Таблица, Поля);
	
	ЗаписьТекста.ЗаписатьСтроку(СтрШаблон(ШаблонСтроки,
								ДополнитьПробелами("Код", 6),
								ДополнитьПробелами("Наименование", сткМаксДлины.Наименование),
								ДополнитьПробелами("Количество", 10),
								ДополнитьПробелами("Последние изменения", 19)));

	ШаблонСФорматом = "|:%1:| %2 |:%3:|:%4:|";
	ЗаписьТекста.ЗаписатьСтроку(СтрШаблон(ШаблонСФорматом,
								СтроковыеФункции.СформироватьСтрокуСимволов("-", 6),
								СтроковыеФункции.СформироватьСтрокуСимволов("-", сткМаксДлины.Наименование),
								СтроковыеФункции.СформироватьСтрокуСимволов("-", 10),
								СтроковыеФункции.СформироватьСтрокуСимволов("-", 19)));
		
	Для Каждого Раздел Из Таблица Цикл
		
		СформироватьВыводРаздела(Раздел);

		ТаблицаПроектов = Раздел.ТаблицаПроектов;
		сткДанныеРаздела = ДанныеРаздела(ТаблицаПроектов);

		Наименование = Раздел.Наименование;

		Строка  = СтрШаблон(ШаблонСтроки, 
					ДополнитьПробелами("[" + Раздел.Код + "]", 6),
					ДополнитьПробелами(Наименование, сткМаксДлины.Наименование), 
					ДополнитьПробелами(сткДанныеРаздела.Количество, 10), 
					ДополнитьПробелами(Формат(сткДанныеРаздела.ПоследняяДата, "ДФ=dd.MM.yyyy"), 19));	
		ЗаписьТекста.ЗаписатьСтроку(Строка);

	КонецЦикла;
	
	ЗаписьТекста.ЗаписатьСтроку("");
	// пропишем ссылки
	Для Каждого Раздел Из Таблица Цикл
		ИмяФайла = ОбъединитьПути("group", Раздел.Код + ".md");
		ЗаписьТекста.ЗаписатьСтроку(ДополнитьПробелами("["+Раздел.Код+"]:", 8) + ИмяФайла + "  ");
	КонецЦикла;
	
	ЗаписьТекста.Закрыть();

КонецПроцедуры

Процедура СформироватьВыводРаздела(Раздел)
	
	ИмяФайла = ОбъединитьПути(ПодкаталогРазделов, Раздел.Код + ".md"); 
	Наименование = Раздел.Наименование;
	ТаблицаПроектов = Раздел.ТаблицаПроектов;

	ЗаписьТекста = Новый ЗаписьТекста(ИмяФайла, КодировкаТекста.UTF8);
	ЗаписьТекста.ЗаписатьСтроку(Наименование);
	ЗаписьТекста.ЗаписатьСтроку("===");
	ЗаписьТекста.ЗаписатьСтроку("");
	ШаблонСтроки = "| %1%2 | %3 | %4 |";
	
	Поля = "НомерСтроки,Наименование,Релиз,Дата";
	сткМаксДлины = ВычислитьМаксимальныеДлиныПолей(ТаблицаПроектов, Поля);
	сткМаксДлины.НомерСтроки = сткМаксДлины.НомерСтроки + 8; 
	
	ЗаписьТекста.ЗаписатьСтроку(СтрШаблон(ШаблонСтроки, 
								ДополнитьПробелами("", сткМаксДлины.НомерСтроки), 
								ДополнитьПробелами("Наименование", сткМаксДлины.Наименование), 
								ДополнитьПробелами("Релиз", сткМаксДлины.Релиз), 
								ДополнитьПробелами("Дата", сткМаксДлины.Дата)));
	ЗаписьТекста.ЗаписатьСтроку(СтрШаблон(ШаблонСтроки,
								СтроковыеФункции.СформироватьСтрокуСимволов("-", сткМаксДлины.НомерСтроки),
								СтроковыеФункции.СформироватьСтрокуСимволов("-", сткМаксДлины.Наименование),
								СтроковыеФункции.СформироватьСтрокуСимволов("-", сткМаксДлины.Релиз),
								СтроковыеФункции.СформироватьСтрокуСимволов("-", сткМаксДлины.Дата)));

	Для Каждого Проект Из ТаблицаПроектов Цикл
		Строка = СтрШаблон(ШаблонСтроки, 
				ДополнитьПробелами("[>>>]["+Проект.НомерСтроки+"] ", сткМаксДлины.НомерСтроки), 
				ДополнитьПробелами(Проект.Наименование, сткМаксДлины.Наименование), 
				ДополнитьПробелами(Проект.Релиз, сткМаксДлины.Релиз),
				ДополнитьПробелами(Проект.Дата, сткМаксДлины.Дата));
		ЗаписьТекста.ЗаписатьСтроку(Строка);
	КонецЦикла;
	
	ЗаписьТекста.ЗаписатьСтроку("");
	АдресСайта = ПараметрыПриложения.АдресСайта();
	Для Каждого Проект Из ТаблицаПроектов Цикл
		ЗаписьТекста.ЗаписатьСтроку("["+Проект.НомерСтроки+"]: " + АдресСайта + Проект.СсылкаПроекта + "  ");
	КонецЦикла;

	ЗаписьТекста.Закрыть();

КонецПроцедуры

Функция ДополнитьПробелами(Знач Строка, ДоДлины)
	Длина = СтрДлина(Строка);
	КоличествоПробелов = Макс(0, ДоДлины - Длина);
	Пробелы = СтроковыеФункции.СформироватьСтрокуСимволов(" ", КоличествоПробелов);
	Возврат Строка(Строка) + Пробелы;
КонецФункции

Функция ВычислитьМаксимальныеДлиныПолей(Коллекция, Поля)
	
	сткМаксДлины = Новый Структура(Поля);
	Для каждого КЗ Из сткМаксДлины Цикл
		сткМаксДлины.Вставить(КЗ.Ключ, 0);
	КонецЦикла;

	Для Каждого Проект Из Коллекция Цикл
		Для каждого КЗ Из сткМаксДлины Цикл
			сткМаксДлины[КЗ.Ключ] = Макс(КЗ.Значение, СтрДлина(Проект[КЗ.Ключ]));
		КонецЦикла;
	КонецЦикла;

	Возврат сткМаксДлины;

КонецФункции

Функция ДанныеРаздела(Таблица)
	
	Количество = Таблица.Количество();
	ПоследняяДата = Дата(1, 1, 1);
	Для каждого Проект Из Таблица Цикл
		ПоследняяДата = Макс(ПоследняяДата, Служебный.ДатаИзСтроки(Проект.Дата));
	КонецЦикла;
	
	Возврат Новый Структура("Количество, ПоследняяДата", Количество, ПоследняяДата);
	
КонецФункции
